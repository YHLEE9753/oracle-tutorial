# 4. Cursor variables
- cursor variable 은 cursor 를 참조하는 variable 이다
- implicit and explicit cursor 와는 다르다
- cursor variable 은 특정 쿼리에 묶이지 않는다
- cursor variable 은 어느 쿼리에도 열려있다는 의미이다.
- cursor variable 의 가장 큰 이점은 PL/SQL 프로그램 간에 쿼리 결과를 전달할 수 있다는 점이다
- 커서 변수가 없으면 커서에서 모든 데이터를 가져와 변수(ex)Collection)에 저장하고 이 변수를 인수로 전달한다.
- cursor variable 을 사용하면 해당 커서에 대한 참조를 전달하기만 하면 된다.
- cursor variable 를 선언하기 위해 REF CURSOR 를 사용한다.
- REF CURSOR 는 2가지 타입이 존재한다. typeS:strong typed , weak type

```oracle-sql
 DECLARE
    TYPE customer_t IS REF CURSOR RETURN customers%ROWTYPE;
    c_customer customer_t;
```
- strong REF CURSOR 의 예시이다.
- cursor variable 은 항상 특정 record structure or type  과 연관된다.

```oracle-sql
DECLARE
    TYPE customer_t IS REF CURSOR;
    c_customer customer_t;
```
- 특정 structure 에 연결 되지 않은 weak typed REF CURSOR 를 선언하는 예시이다.
```oracle-sql
 DECLARE
    c_customer SYS_REFCURSOR;
```
- 오라클 9 부터는 predefined REF CURSOR 인 SYS_REFCURSOR 를 사용할 수 있다.

## PL/SQL cursor variable examples
```oracle-sql
CREATE OR REPLACE FUNCTION get_direct_reports(
      in_manager_id IN employees.manager_id%TYPE)
   RETURN SYS_REFCURSOR
AS
   c_direct_reports SYS_REFCURSOR;
BEGIN

   OPEN c_direct_reports FOR 
   SELECT 
      employee_id, 
      first_name, 
      last_name, 
      email
   FROM 
      employees 
   WHERE 
      manager_id = in_manager_id 
   ORDER BY 
         first_name,   
         last_name;

   RETURN c_direct_reports;
END;
```
- 이 함수는 employees  테이브렝서 manager id 를 기반으로 all direct reports 를 가져온다.
- function 은 weak typed REF CURSOR 변수를 반환한다.
```oracle-sql
 DECLARE
   c_direct_reports SYS_REFCURSOR;
   l_employee_id employees.employee_id%TYPE;
   l_first_name  employees.first_name%TYPE;
   l_last_name   employees.last_name%TYPE;
   l_email       employees.email%TYPE;
BEGIN
   -- get the ref cursor from function
   c_direct_reports := get_direct_reports(46); 
   
   -- process each employee
   LOOP
      FETCH
         c_direct_reports
      INTO
         l_employee_id,
         l_first_name,
         l_last_name,
         l_email;
      EXIT
   WHEN c_direct_reports%notfound;
      dbms_output.put_line(l_first_name || ' ' || l_last_name || ' - ' ||    l_email );
   END LOOP;
   -- close the cursor
   CLOSE c_direct_reports;
END;
/
```
- 위 anonymous block 은 get_direct_reports() 함수를 호출한다.
- cursor variable 를 사용하여 46 id 의 관리자의 direct reports 를 보여준다
