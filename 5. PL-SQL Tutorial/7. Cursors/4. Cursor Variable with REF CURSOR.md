# 4. Cursor Variable with REF CURSOR
- cursor variable 은 cursor 를 reference 하는 변수이다
- implicit cursor, explicit cursor 와는 다르며 cursor variable 은 any specific query 에 묶이지 않는다.
- 이는 cursor variable 이 어느 query 에서든 열릴 수 있다는 말이다.
- cursor variable 을 선언하기 위해 REF CURSOR 를 사용한다.
- PL/SQL 은 REF CURSOR 를 2가지 form 으로 가지고 있다 : String type, Weak type

## syntax
```oracle-sql
DECLARE
    TYPE customer_t IS REF CURSOR RETURN customers%ROWTYPE;
    c_customer customer_t;
```
- 이러한 형태의 cursor variable 을 string typed REF CURSOR 라고 부른다.
- cursor variable 이 항상 특정한 record structure 혹은 type 과 연관되기 때문이다.
```oracle-sql
DECLARE
    TYPE customer_t IS REF CURSOR;
    c_customer customer_t;

```
- 이러한 형태의 cursor variable 을 weak typed REF CURSOR 라고 부른다.
```oracle-sql
DECLARE
    c_customer SYS_REFCURSOR;
```
- 오라클 9i 부터는 미리 정의 된 weak typed REF CURSOR 를 가리키는 SYS_REFCURSOR 를 사용하여  를 이용해 weak REF CURSOR 를 만들수 있따.

## example
```oracle-sql
CREATE OR REPLACE FUNCTION get_direct_reports(
      in_manager_id IN employees.manager_id%TYPE)
   RETURN SYS_REFCURSOR
AS
   c_direct_reports SYS_REFCURSOR;
BEGIN

   OPEN c_direct_reports FOR 
   SELECT 
      employee_id, 
      first_name, 
      last_name, 
      email
   FROM 
      employees 
   WHERE 
      manager_id = in_manager_id 
   ORDER BY 
         first_name,   
         last_name;

   RETURN c_direct_reports;
END;
```
- employees 테이블에서 관리자 id 를 기반으로 관리자의 모든 보고서를 가져온다.

```oracle-sql
DECLARE
   c_direct_reports SYS_REFCURSOR;
   l_employee_id employees.employee_id%TYPE;
   l_first_name  employees.first_name%TYPE;
   l_last_name   employees.last_name%TYPE;
   l_email       employees.email%TYPE;
BEGIN
   -- get the ref cursor from function
   c_direct_reports := get_direct_reports(46); 
   
   -- process each employee
   LOOP
      FETCH
         c_direct_reports
      INTO
         l_employee_id,
         l_first_name,
         l_last_name,
         l_email;
      EXIT
   WHEN c_direct_reports%notfound;
      dbms_output.put_line(l_first_name || ' ' || l_last_name || ' - ' ||    l_email );
   END LOOP;
   -- close the cursor
   CLOSE c_direct_reports;
END;
/
```


