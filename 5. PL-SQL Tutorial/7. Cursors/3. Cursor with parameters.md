# 3. Cursor with parameters
- 매개변수와 PL/SQL 커서를 이용하여 매개변수 기반으로 데이터를 가져오는 방법을 알아본다
- explicit cursor 는 list of parameters 를 허용한다.
- cursor 를 open 할 때 마다, cursor 에 다른 arguments 를 pass 할 수 있고 이는 각기 다른 result sets 결과물로 이어진다.

## syntax
```oracle-sql
CURSOR cursor_name (parameter_list) 
IS
cursor_query;
```
- 커서 쿼리에서 parameter_list 의 각 매개변수는 상수가 사용되는 모든 곳에서 사용할 수 있습니다. 
- 커서 매개 변수는 커서 쿼리 외부에서 참조할 수 없습니다.

```oracle-sql
OPEN cursor_name (value_list);
```
- 매개변수가 있는 커서를 열기 위한 구문이다
- 이 구문에서는 커서의 매개변수에 해당하는 인수를 전달한다
- 매개변수가 있는 커서는 parameterized cursors 라고 한다.

## example
### PL/SQL cursor with parameters example
```oracle-sql
DECLARE
    r_product products%rowtype;
    CURSOR c_product (low_price NUMBER, high_price NUMBER)
    IS
        SELECT *
        FROM products
        WHERE list_price BETWEEN low_price AND high_price;
BEGIN
    -- show mass products
    dbms_output.put_line('Mass products: ');
    OPEN c_product(50,100);
    LOOP
        FETCH c_product INTO r_product;
        EXIT WHEN c_product%notfound;
        dbms_output.put_line(r_product.product_name || ': ' ||r_product.list_price);
    END LOOP;
    CLOSE c_product;

    -- show luxury products
    dbms_output.put_line('Luxury products: ');
    OPEN c_product(800,1000);
    LOOP
        FETCH c_product INTO r_product;
        EXIT WHEN c_product%notfound;
        dbms_output.put_line(r_product.product_name || ': ' ||r_product.list_price);
    END LOOP;
    CLOSE c_product;

END;
/
```

### PL/SQL parameterized cursor with default values
```oracle-sql
CURSOR cursor_name (
    parameter_name datatype := default_value, 
    parameter_name datatype := default_value, 
    ...
) IS 
    cursor_query;
```
- 인수를 전달하지 않고 parameterized cursor 를 열면 커서는해당 매개변수의 기본값을 사용한다.

```oracle-sql
DECLARE
    CURSOR c_revenue (in_year NUMBER :=2017 , in_customer_id NUMBER := 1)
    IS
        SELECT SUM(quantity * unit_price) revenue
        FROM order_items
        INNER JOIN orders USING (order_id)
        WHERE status = 'Shipped' AND EXTRACT( YEAR FROM order_date) = in_year
        GROUP BY customer_id
        HAVING customer_id = in_customer_id;
        
    r_revenue c_revenue%rowtype;
BEGIN
    OPEN c_revenue;
    LOOP
        FETCH c_revenue INTO r_revenue;
        EXIT    WHEN c_revenue%notfound;
        -- show the revenue
        dbms_output.put_line(r_revenue.revenue);
    END LOOP;
    CLOSE c_revenue;
END;
```

