# 5. Updatable cursor
- 프로그램에서 update 하기 전에 set of rows 을 lock 하려고 하는 경우가 있다.
- Oracle 은 이러한 종류의 lock 메커니즘을 수행하기 위해 updatable cursor 에 SELECT 의 FOR UPDATE 구문을 제공한다. 
## syntax
```oracle-sql
CURSOR cursor_name IS
    SELECT select_clause
    FROM from_clause
    WHERE where_clause
    FOR UPDATE;
```
- 커서를 open 하면 select ... for update 에 지정된 테이블의 명령문에 의해 선택된 모든 행을 lock 한다.
- 이 rows 들은 커서가 close 되기 전까지 혹은 트랜잭션이 completed , commit, rollback 되기 전까지 lock 되어 있다.

```oracle-sql
CURSOR cursor_name IS
    SELECT select_clause
    FROM from_clause
    WHERE where_clause
    FOR UPDATE OF column_name;
```
- update 하고 싶은 특정 column 이 존재한 경우 FOR UPDATE 다음에 OF 절을 사용할 수 있따.
- FOR UPDATE OF 절 에 있는 열 이름이 있는 테이블의 행만 잠금다.
- FOR UPDATE 절만 사용하는 경우와 OF 키워드 이후에 하나 이상의 열을 컬럼을 포함시키지 않으면  오라클은 FROM 절에 나열된 모든 테이블에서 선택한 모든 행을 lock 한다.

## example
### Oracle Cursor FOR UPDATE example
```oracle-sql
DECLARE
    -- customer cursor
    CURSOR c_customers IS 
        SELECT 
            customer_id, 
            name, 
            credit_limit
        FROM 
            customers
        WHERE 
            credit_limit > 0 
        FOR UPDATE OF credit_limit;
    -- local variables
    l_order_count PLS_INTEGER := 0;
    l_increment   PLS_INTEGER := 0;
    
BEGIN
    FOR r_customer IN c_customers
    LOOP
        -- get the number of orders of the customer
        SELECT COUNT(*)
        INTO l_order_count
        FROM orders
        WHERE customer_id = r_customer.customer_id;
        -- 
        IF l_order_count >= 5 THEN
            l_increment := 5;
        ELSIF l_order_count < 5 AND l_order_count >=2 THEN
            l_increment := 2;
        ELSIF l_increment = 1 THEN
            l_increment := 1;
        ELSE 
            l_increment := 0;
        END IF;
        
        IF l_increment > 0 THEN
            -- update the credit limit
            UPDATE 
                customers
            SET 
                credit_limit = credit_limit * ( 1 +  l_increment/ 100)
            WHERE 
                customer_id = r_customer.customer_id;
            
            -- show the customers whose credits are increased
            dbms_output.put_line('Increase credit for customer ' 
                || r_customer.NAME || ' by ' 
                || l_increment || '%' );
        END IF;
    END LOOP;
    
    EXCEPTION
        WHEN OTHERS THEN
            dbms_output.put_line('Error code:' || SQLCODE);
            dbms_output.put_line('Error message:' || sqlerrm);
            RAISE;
            
END;
/

```
- 먼저 크레딧이 0보다 큰 고객의 크레딧을 업데이트하는 업데이트 가능한 커서를 선언합니다.
- 다음으로 커서의 행을 반복합니다.
- 그런 다음 각 고객의 주문 수를 가져옵니다.
- 그런 다음 주문 수에 따라 크레딧 증분을 할당합니다.
- 마지막으로 고객의 신용을 업데이트합니다.