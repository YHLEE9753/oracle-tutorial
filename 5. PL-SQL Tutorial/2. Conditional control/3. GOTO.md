# 3. GOTO
## syntax
- goto 를 사용하면 레이블이 지정된 블록 또는 명령문으로 제어를 전송할 수 있다.
```oracle-sql
GOTO label_name;

<<label_name>>;
```
- `label_name`은 대상 명령문을 식별하는 레이블의 이름이다. 
- 이중 꺽쇠 괄호로 레이블 이름을 묶는다.
## example
```oracle-sql
BEGIN
  GOTO second_message;

  <<first_message>>
  DBMS_OUTPUT.PUT_LINE( 'Hello' );
  GOTO the_end;

  <<second_message>>
  DBMS_OUTPUT.PUT_LINE( 'PL/SQL GOTO Demo' );
  GOTO first_message;

  <<the_end>>
  DBMS_OUTPUT.PUT_LINE( 'and good bye...' );

END;
---------- 출력 결과
PL/SQL GOTO Demo
Hello
and good Bye...
```

## PL/SQL GOTO statement restrictions
```oracle-sql
DECLARE 
  n_sales NUMBER;
  n_tax NUMBER;
BEGIN 
    GOTO inside_if_statement;
    IF n_sales > 0 THEN
      <<inside_if_statement>>
      n_tax  := n_sales * 0.1;
    END IF;
END;
```
- 1. goto 를 사용하여 if, case, loop, sub-block 내부로 control 을 전달할 수는 없다
- 위 코드는 PLS-00375 에러를 발생시킨다.

```oracle-sql
DECLARE
  n_sales      NUMBER;
  n_commission NUMBER;
BEGIN
  n_sales := 120000;
  IF n_sales      > 100000 THEN
    n_commission := 0.2;
    GOTO zero_commission;
  elsif n_sales   > 50000 THEN
    n_commission := 0.15;
  elsif n_sales   > 20000 THEN
    n_commission := 0.1;
  ELSE
    <<zero_commission>>
    n_commission := 0;
  END IF;
END;
```
- 2. goto 를 사용하여 if 에서 elsif 혹은 else when case 등 control 를 전달할 수 없다.
- PLS-00375 에러를 발생시킨다.


