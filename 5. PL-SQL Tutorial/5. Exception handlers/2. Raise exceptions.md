# 2. Raise exceptions
- PL/SQL 를 사용하여 user-defined exception, internally defined exception 을 발생시키고 exception 을 다시 발생시키는 `RAISE` 문에 대해 알아보자
- 예외를 explicitly 명시적으로 발생시키기 위해 `RAISE` 문을 활용해야 한다.
  - Raise a user-defined exception.
  - Raise an internally defined exception.
  - Reraising the current exception.

## Raising a user-defined exception
### Declaring a user-defined exception
```oracle-sql
DECLARE
    exception_name EXCEPTION;
```
- 변수 선언과 마찬가지로 block 의 declare 부분에서 예외를 선언한다.
- 사용자 저의 예외에는  error_code 가 할당되어야 한다. 
- 이를 위해 EXCEPTION_INIT 과 PRAGMA 를 같이 사용한다.
```oracle-sql
PRAGMA EXCEPTION_INIT (exception_name, error_code)
```
- error_code 는 -20,999 ~ -20,000 사이의 integer 값이어야 한다.
- message 는 최대 2,048 bytes 의 길이를 가진 character string 이다.

```oracle-sql
DECLARE
    exception_name EXCEPTION;
    PRAGMA EXCEPTION_INIT (exception_name, error_number);
```
- 전체 syntax 는 다음과 같다.

### Raising a user-defined exception example
```oracle-sql
DECLARE
    e_credit_too_high EXCEPTION;
    PRAGMA exception_init( e_credit_too_high, -20001 );
    l_max_credit customers.credit_limit%TYPE;
    l_customer_id customers.customer_id%TYPE := &customer_id;
    l_credit customers.credit_limit%TYPE := &credit_limit;
BEGIN
    -- get the max credit limit
    SELECT MAX(credit_limit) 
    INTO l_max_credit
    FROM customers;
    
    -- check if input credit is greater than the max credit
    IF l_credit > l_max_credit THEN 
        RAISE e_credit_too_high;
    END IF;
    
    -- if not, update credit limit
    UPDATE customers 
    SET credit_limit = l_credit
    WHERE customer_id = l_customer_id;
    
    COMMIT;
END;
/
```
- e_credit_too_high 라는 user-defined exception 을 선언하고 이를 에러코드 -20001 과 연관시킨다.
- MAX() 를 이용해 customers 테이블에서 maximum credit 을 select 하고 이를 l_max_credit 변수에 할당한다.
- input credit 이 max 보다 크면 e_credit_too_high 예외를 발생시킨다.
- 마지막으로 예외가 안발생하면 credit 값을 업데이트 한다.
- 만냑 예외 메세지를 포함시키고 싶으면
```oracle-sql
RAISE e_credit_too_high;
-- 대신에
raise_application_error(-20001,'Credit is too high');
-- 를 사용하면 예외메세지가 포함된다.
```

### Raising an internally defined exception
- runtime system 은 internally defined exception 가 발생할 때 implicitly 하게 발생시킨다.
- 추가로 explictly 하게 internally defined exception 을 발생시킬 수 있는데 exception 이 이름을 가지고 있을때 RAISE 구문을 사용하여 발생시킨다
```oracle-sql
RAISE exception_name;
```
```oracle-sql
DECLARE
    l_customer_id customers.customer_id%TYPE := &customer_id;
BEGIN
    -- get the meax credit limit
    IF l_customer_id < 0 THEN
        RAISE invalid_number;
    END IF;
END;
/
```
- invalid_number 는 internally defined exception 이다
- input customer id 로 -10 을 입력하면 ORA-01722: invalid number 예외가 발생한다.

### Reraising the current exception
- raise 문을 통해 현재 예외를 re-raise 시킬 수 있다.
- 예외를 다시 발생시키면 나중에 처리할 수 있는 encloing block 으로 예외를 전달한다.
- 예외를 다시 발생시키려고 예외 이름을 특별히 지정할 필요는 없다.
``` oracle-sql
DECLARE
    e_credit_too_high EXCEPTION;
    PRAGMA exception_init( e_credit_too_high, -20001 );
    l_max_credit customers.credit_limit%TYPE;
    l_customer_id customers.customer_id%TYPE := &customer_id;
    l_credit customers.credit_limit%TYPE     := &credit_limit;
BEGIN
    BEGIN
        -- get the max credit limit
        SELECT MAX(credit_limit) 
        INTO l_max_credit
        FROM customers;
        
        -- check if input credit is greater than the max credit
        IF l_credit > l_max_credit THEN 
            RAISE e_credit_too_high;
        END IF;
        EXCEPTION
            WHEN e_credit_too_high THEN
                dbms_output.put_line('The credit is too high' || l_credit);
                RAISE; -- reraise the exception
    END;
EXCEPTION
    WHEN e_credit_too_high THEN
        -- get average credit limit
        SELECT avg(credit_limit) 
        into l_credit
        from customers;
        
        -- adjust the credit limit to the average
        dbms_output.put_line('Adjusted credit to ' || l_credit);
    
        --  update credit limit
        UPDATE customers 
        SET credit_limit = l_credit
        WHERE customer_id = l_customer_id;
   
        COMMIT;
END;
/ 
```
- customer table 에서 max credit limit 값을 가져온다
- 최대 크래딧을 사용자 입력 크레딧과 비교한다
- 사용자 입력 크레딧이 최대 크레딧보다 큰 경우 e_credit_too_high 예외를 발생시킨다
- 내부 블록에 예외처리 부분의 예외를 reraise 시키고 메세지를 출력한다
- 밖 블록에서 l_credit 변수에 평균 credit 을 재 할당하고 새로 조정된 credit 을 customer 에 update 한다.
- 만약 customer id 를 100, credit limit 을 10000 지정하면, customer 의 credit limit 은 평균 credit 으로 업데이트 된다.

```oracle-sql
SELECT * FROM customers 
WHERE customer_id = 100;
```



