# 5. Handling other unhandled exceptions
## syntax
```oracle-sql
EXCEPTION
    ...
    WHEN OTHERS
        -- catch other exceptions
```
- when others 절에서 처리되지 않은 예외를 catch 하는 절을 포함할 수 있다.
- when others 절에서 non-specific 예외를 처리하기 때문에 SQLCODE 나 SELERRM 같은 built-in error functions 를 활용해야 한다.
- SQL 문에저 직접 SQLCODE 나 SQLERRM function 을 사용할 수는 없다
- 대신 반드시 variables 에 returned values 를 할당한 후 SQL 문에서 variables 로 사용해야 한다.

## example
### SQLCODE function
- SQLCODE 함수는 인수를 허용하지 않으며 가장 최근 exception 의 number code 를 반환한다.
- internal exception 인 경우, SQLCODE 는 +100 코드인 NO_DATA_FOUND 를 제외하고 음수 값을 return 한다.
- user-defined exception 인 경우, SQLCODE 는 pragma exception_init 를 통한 exception 과 관련된 숫자나 +1 를 반환한다.
- SQLCODE 는 exception-handling section 에서만 usable 하다. 
- 만약 SQLCODE 를 exception handler 밖에서 사용하면, 항상 0을 return 한다.

```oracle-sql
DECLARE
    l_code NUMBER;
    r_customer customers%rowtype;
BEGIN
    SELECT * INTO r_customer FROM customers;
    
    EXCEPTION 
        WHEN OTHERS THEN
        l_code := SQLCODE;  
        dbms_output.put_line('Error code:' || l_code);
END;
/
```
- 너무 많은 row 를 가져오기 때문에 -1422 에러를 발생시킨다.
- 출력한다.

## SQLERRM function
```oracle-sql
SQLERRM([error_number])
```
- SQLERRM 은 error number 를 인수로 가진다. 
- 해당 오류 번호와 관련된 오류 메시지를 반환한다.
- error_number : 유효한 모든 오라클 오류 번호
  - error_number 을 생략한경우 SQLCODE 의 현재 값과 관련된 오류 메세지를 반환한다.
- argument 없는 SQLERRM 기능은 exception handler 에서만 유효하다
```oracle-sql
 DECLARE
    l_msg VARCHAR2(255);
    r_customer customers%rowtype;
BEGIN
    SELECT * INTO r_customer FROM customers;
    
    EXCEPTION 
        WHEN OTHERS THEN
        l_msg := SQLERRM;  
        dbms_output.put_line(l_msg);
END;
/
--------------------------
ORA-01422: exact fetch returns more than requested number of rows
```
- 에러메세지를 출력한다

## Using SQLCODE and SQLERRM functions example
```oracle-sql
DECLARE
    l_first_name  contacts.first_name%TYPE := 'Flor';
    l_last_name   contacts.last_name%TYPE := 'Stone';
    l_email       contacts.email%TYPE := 'flor.stone@raytheon.com';
    l_phone       contacts.phone%TYPE := '+1 317 123 4105';
    l_customer_id contacts.customer_id%TYPE := -1;
BEGIN
    -- insert a new contact
    INSERT INTO contacts(first_name, last_name, email, phone, customer_id)
    VALUES(l_first_name, l_last_name, l_email, l_phone, l_customer_id);
    
    EXCEPTION 
        WHEN OTHERS THEN
            DECLARE
                l_error PLS_INTEGER := SQLCODE;
                l_msg VARCHAR2(255) := sqlerrm;
            BEGIN
                CASE l_error 
                WHEN -1 THEN
                    -- duplicate email
                    dbms_output.put_line('duplicate email found ' || l_email);
                    dbms_output.put_line(l_msg);
                    
                WHEN -2291 THEN
                    -- parent key not found
                    dbms_output.put_line('Invalid customer id ' || l_customer_id);
                    dbms_output.put_line(l_msg);
                END CASE;
                -- reraise the current exception
                RAISE;
            END;
            
END;
/
```
- contacts 테이블에 new contact 를 insert  하는 예시이다
- where others 절에 exception handler 를 가진다
- 위에 예외 처리기는 2가지 예외 -1(중복 이메일), -2291(부모키를 찾을 수 없음) 를 trap 한다.
- 그여기서 사용자 지정 메세지를 보여주고 raise 문을 통해 exception 을 reraises 한다.




